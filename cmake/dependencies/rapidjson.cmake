#############
# RapidJSON #
#############

set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR}/modules/ ${CMAKE_MODULE_PATH})
include(FetchContent)
include(ExternalProject)
cmake_policy(SET CMP0079 NEW)

# Define some variables to allow co fetch content declarations
set(RapidJSON_GIT_REMOTE "https://github.com/Tencent/rapidjson.git")
# a95e013b97ca6523f32da23f5095fcc9dd6067e5 is the last commit before a change which breaks our method of finding rapid json without running a cmake install first.
# but we also need to patch this to avoid a cmake >= 3.26.4 deprecation
set(RapidJSON_GIT_TAG "a95e013b97ca6523f32da23f5095fcc9dd6067e5")

# Head of master as of 2020-07-14, as last release is ~500 commits behind head
if(NOT CMAKE_CXX_COMPILER_ID STREQUAL "NVHPC")
    FetchContent_Declare(
        rapidjson
        GIT_REPOSITORY ${RapidJSON_GIT_REMOTE}
        GIT_TAG ${RapidJSON_GIT_TAG}
        GIT_PROGRESS   OFF
        PATCH_COMMAND git apply ${CMAKE_CURRENT_LIST_DIR}/patches/rapidjson-cmake-3.5-deprecation.patch || true
    )
else()
    FetchContent_Declare(
        rapidjson
        GIT_REPOSITORY ${RapidJSON_GIT_REMOTE}
        GIT_TAG ${RapidJSON_GIT_TAG}
        GIT_PROGRESS   OFF
        PATCH_COMMAND git apply ${CMAKE_CURRENT_LIST_DIR}/patches/rapidjson-cmake-3.5-deprecation.patch ${CMAKE_CURRENT_LIST_DIR}/patches/rapidjson-nvhpc.patch || true
    )
endif()
FetchContent_GetProperties(rapidjson)
if(NOT rapidjson_POPULATED)
    FetchContent_Populate(rapidjson)
    # RapidJSON has custom RapidJSONConfig.cmake, generated by cmake configure/generate
    execute_process(
    COMMAND ${CMAKE_COMMAND} . -DRAPIDJSON_BUILD_DOC=OFF -DRAPIDJSON_BUILD_EXAMPLES=OFF -DRAPIDJSON_BUILD_TESTS=OFF -DRAPIDJSON_BUILD_THIRDPARTY_GTEST=OFF -Wno-deprecated
    WORKING_DIRECTORY "${rapidjson_SOURCE_DIR}"
    OUTPUT_QUIET
    )
    set(CMAKE_PREFIX_PATH "${CMAKE_PREFIX_PATH};${rapidjson_SOURCE_DIR}")
    find_package(RapidJSON REQUIRED
        PATHS ${rapidjson_SOURCE_DIR}
            NO_CMAKE_PATH
            NO_CMAKE_ENVIRONMENT_PATH
            NO_SYSTEM_ENVIRONMENT_PATH
            NO_CMAKE_PACKAGE_REGISTRY
            NO_CMAKE_SYSTEM_PATH)
    # Include path is ${RapidJSON_INCLUDE_DIRS}

    # Create a namespaced alias target
    if(TARGET rapidjson)
        add_library(RapidJSON::rapidjson ALIAS rapidjson)
    endif()
endif()

# Mark some CACHE vars advanced for a cleaner GUI
mark_as_advanced(RapidJSON_DIR)
mark_as_advanced(FETCHCONTENT_SOURCE_DIR_RAPIDJSON)
mark_as_advanced(FETCHCONTENT_QUIET)
mark_as_advanced(FETCHCONTENT_BASE_DIR)
mark_as_advanced(FETCHCONTENT_FULLY_DISCONNECTED)
mark_as_advanced(FETCHCONTENT_UPDATES_DISCONNECTED) 
mark_as_advanced(FETCHCONTENT_UPDATES_DISCONNECTED_RAPIDJSON) 